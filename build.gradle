buildscript {
    repositories {
        mavenCentral()
        //Needed only for SNAPSHOT versions
        //maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0"
    }
}

ext {
    awsSdkVersion = "2.5.14"
    springVersion = "5.1.4.RELEASE"
    springBootVersion = "2.1.2.RELEASE"
    edisonVersion = "2.0.0-rc4"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'io.codearte.nexus-staging'
apply from: "${rootDir}/jacoco.gradle"

task startLocalStack(type:Exec) {
    commandLine "docker", "run", "--name", "synapse_localstack", "-d", "-p", "4567-4583:4567-4583", "localstack/localstack:latest"
}
task stopLocalStack(type: Exec) {
    commandLine 'docker', 'rm', '-f', 'synapse_localstack'
}

nexusStaging {
    username=sonatypeUsername
    password=sonatypePassword
    packageGroup='de.otto'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply from: "${rootDir}/jacoco.gradle"
    apply from: "${rootDir}/signing.gradle"

    dependencies {
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"
    }

    compileJava.dependsOn(processResources)

    version = '0.16.4-SNAPSHOT'
    group = 'de.otto.synapse'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

}


