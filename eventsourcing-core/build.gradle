apply plugin: 'java-library'

dependencies {
    implementation "de.otto.edison:edison-aws-s3:0.2.9-SNAPSHOT"

    implementation "software.amazon.awssdk:s3:${awsSdkVersion}"
    implementation "software.amazon.awssdk:kinesis:${awsSdkVersion}"

    implementation "org.springframework:spring-context:${springVersion}"
    implementation 'org.springframework.security:spring-security-core:4.2.3.RELEASE'
    implementation "org.springframework.retry:spring-retry:1.2.1.RELEASE"

    implementation "org.springframework:spring-aop:${springVersion}"
    implementation "org.aspectj:aspectjweaver:1.8.13"

    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.3"

    implementation("net.openhft:chronicle-map:3.14.1") {
        exclude group: "com.intellij", module: "annotations"
    }

//    // This dependency is exported to consumers, that is to say found on their compile classpath.
//    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:23.0'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testImplementation "com.jayway.jsonpath:json-path-assert:2.2.0"
    testImplementation 'com.tngtech.java:junit-dataprovider:1.13.1'
    testImplementation "org.awaitility:awaitility:3.0.0"
}

apply plugin: 'maven'

jar {
    manifest.attributes provider: 'gradle'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

apply plugin: 'signing'

signing {
    sign configurations.archives
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

task startLocalStack(type:Exec) {
    commandLine "docker", "run", "--name", "localstack_for_testing", "-d", "-p", "4567-4583:4567-4583", "-p", "8080:8080", "localstack/localstack:latest"
}
task stopLocalStack(type: Exec) {
    commandLine 'docker', 'rm', '-f', 'localstack_for_testing'
}

test.dependsOn(startLocalStack)
test.finalizedBy(stopLocalStack)



uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'eventsourcing-core'
                packaging 'jar'
                description 'A library used at otto.de to implement Spring Boot based event-sourcing microserivces.'
                url 'http://github.com/otto-de/edison-eventsourcing'

                scm {
                    url 'scm:git@github.com:otto-de/edison-eventsourcing.git'
                    connection 'scm:git@github.com:otto-de/edison-eventsourcing.git'
                    developerConnection 'scm:git@github.com:otto-de/edison-eventsourcing.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'gsteinacker'
                        name 'Guido Steinacker'
                    }
                    developer {
                        id 'mbargmann'
                        name 'Matthias Bargmann'
                    }
                    developer {
                        id 'fbregulla'
                        name 'Frank Bregulla'
                    }
                    developer {
                        id 'ftorkler'
                        name 'Florian Torkler'
                    }
                    developer {
                        id 'miregel'
                        name 'Micha Regel'
                    }
                    developer {
                        id 'hstruebe'
                        name 'Henning Strueber'
                    }
                }
            }
        }
    }
}
